apply plugin: "com.android.application"
apply plugin: "kotlin-android"
apply plugin: "com.neenbedankt.android-apt"
apply plugin: "me.tatarka.retrolambda"

android {
	compileSdkVersion 25
	buildToolsVersion "25.0.2"

	compileOptions {
		sourceCompatibility JavaVersion.VERSION_1_8
		targetCompatibility JavaVersion.VERSION_1_8
	}

	packagingOptions {
		// Exclude duplicate files in output
		exclude "META-INF/LICENSE"
		exclude "META-INF/NOTICE"
	}

	lintOptions {
		// Fixes dagger errors
		disable "InvalidPackage"
	}

	defaultConfig {
		applicationId "se.gustavkarlsson.skylight.android"
		minSdkVersion 19
		targetSdkVersion 25
		versionCode 1
		versionName "1.0"
		testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
	}

	buildTypes {
		release {
			minifyEnabled true
			proguardFiles getDefaultProguardFile("proguard-android.txt"), "proguard-rules.pro"
		}

		debug {
			versionNameSuffix "-debug"
			applicationIdSuffix ".debug"
		}
	}
}

retrolambda {
	jvmArgs "-noverify"
}

// Stops dagger compiler errors from truncating
gradle.projectsEvaluated {
	tasks.withType(JavaCompile) {
		options.compilerArgs << "-Xmaxerrs" << "500"
	}
}

dependencies {
	ext {
		retrofitVersion = "2.2.0"
		streamsupportVersion = "1.5.4"
		daggerVersion = "2.10"
		powermockVersion = "1.6.6"
		googlePlayServicesVersion = "10.2.4"
		androidSupportVersion = "25.3.1"
	}
	compile "org.jetbrains.kotlin:kotlin-stdlib-jre7:" + kotlinVersion
	compile "com.android.support:appcompat-v7:" + androidSupportVersion
	compile "com.android.support:design:" + androidSupportVersion
	compile "com.android.support.constraint:constraint-layout:1.0.2"
	compile "com.google.android.gms:play-services-location:" + googlePlayServicesVersion
	compile "com.google.android.gms:play-services-gcm:" + googlePlayServicesVersion

	compile "com.evernote:android-job:1.1.10"
	compile "com.github.gustavkarlsson:aurora-notifier-common:aurora-notifier-common-1.5.0"
	compile "net.e175.klaus:solarpositioning:0.0.9"
	compile "com.vincentbrison.openlibraries.android:dualcache:3.1.1"
	compile "com.jakewharton.threetenabp:threetenabp:1.0.5"

	// Retrofit
	compile "com.squareup.retrofit2:retrofit:" + retrofitVersion
	compile "com.squareup.retrofit2:converter-gson:" + retrofitVersion

	// Stream support
	compile "net.sourceforge.streamsupport:streamsupport:" + streamsupportVersion
	compile "net.sourceforge.streamsupport:streamsupport-cfuture:" + streamsupportVersion
	compile "net.sourceforge.streamsupport:streamsupport-atomic:" + streamsupportVersion
	compile "net.sourceforge.streamsupport:streamsupport-flow:" + streamsupportVersion
	compile "net.sourceforge.streamsupport:streamsupport-literal:" + streamsupportVersion

	// Dagger
	compile "com.google.dagger:dagger:" + daggerVersion
	apt "com.google.dagger:dagger-compiler:" + daggerVersion
	provided "javax.annotation:jsr250-api:1.0"

	// Testing
	testCompile "junit:junit:4.12"
	testCompile "org.mockito:mockito-core:1.10.19"
	testCompile "commons-io:commons-io:2.4"
	testCompile "org.assertj:assertj-core:2.5.0"
	testCompile "org.powermock:powermock-api-mockito:" + powermockVersion
	testCompile "org.powermock:powermock-module-junit4-rule-agent:" + powermockVersion
	testCompile "org.powermock:powermock-module-junit4-rule:" + powermockVersion
	testCompile "org.powermock:powermock-module-junit4:" + powermockVersion

	androidTestCompile "com.android.support:support-annotations:" + androidSupportVersion
	androidTestCompile "com.android.support.test:runner:0.5"
	androidTestCompile "com.android.support.test:rules:0.5"
	androidTestCompile "com.android.support.test.espresso:espresso-core:2.2.2"
	androidTestApt "com.google.dagger:dagger-compiler:" + daggerVersion
}
