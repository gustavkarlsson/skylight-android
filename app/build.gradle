apply plugin: "com.android.application"
apply plugin: "kotlin-android"
apply plugin: "kotlin-kapt"
apply plugin: "kotlin-android-extensions"

android {
	compileSdkVersion 26
	buildToolsVersion "26.0.2"

	sourceSets {
		main.java.srcDirs += "src/main/kotlin"
		debug.java.srcDirs += "src/debug/kotlin"
		release.java.srcDirs += "src/release/kotlin"
		beta.java.srcDirs += "src/release/kotlin" // Use release sources for now
		test.java.srcDirs += "src/test/kotlin"
		androidTest.java.srcDirs += "src/androidTest/kotlin"
	}

	packagingOptions {
		// Exclude duplicate files in output
		exclude "META-INF/LICENSE"
		exclude "META-INF/NOTICE"
	}

	lintOptions {
		// Fixes dagger errors
		disable "InvalidPackage"
	}

	testOptions {
		// Solves android Log not mocked
		unitTests.returnDefaultValues = true
	}

	defaultConfig {
		applicationId "se.gustavkarlsson.skylight.android"
		minSdkVersion 19
		targetSdkVersion 26
		versionCode 1
		versionName "1.0"
		testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
	}

	signingConfigs {
		release {
			storeFile file(project.android_store_file)
			storePassword project.android_store_password
			keyAlias project.android_key_alias
			keyPassword project.android_key_password
		}
	}

	buildTypes {
		release {
			minifyEnabled true
			proguardFiles getDefaultProguardFile("proguard-android.txt"), "proguard-rules.pro"
			signingConfig signingConfigs.release
		}

		beta {
			initWith buildTypes.release
			versionNameSuffix "-beta"
		}

		debug {
			versionNameSuffix "-debug"
			applicationIdSuffix ".debug"
		}
	}
}

kotlin {
	experimental {
		coroutines "enable"
	}
}

// Stops dagger compiler errors from truncating
gradle.projectsEvaluated {
	tasks.withType(JavaCompile) {
		options.compilerArgs << "-Xmaxerrs" << "500"
	}
}

dependencies {
	ext {
		retrofit_version = "2.3.0"
		dagger_version = "2.11"
		google_play_services_version = "11.4.2"
		android_support_version = "26.1.0"
		anko_version = "0.10.1"
		mockito_version = "2.10.0"
		mockito_kotlin_version = "1.5.0"
		commons_io_version = "2.4"
	}
	implementation "org.jetbrains.kotlin:kotlin-stdlib-jre7:$kotlin_version"
	implementation "com.android.support:appcompat-v7:$android_support_version"
	implementation "com.android.support:design:$android_support_version"
	implementation "com.android.support.constraint:constraint-layout:1.0.2"
	implementation "com.google.android.gms:play-services-location:$google_play_services_version"
	implementation "com.google.android.gms:play-services-gcm:$google_play_services_version"

	implementation "com.evernote:android-job:1.2.0"
	implementation "com.github.gustavkarlsson:aurora-notifier-common:aurora-notifier-common-1.5.0"
	implementation "net.e175.klaus:solarpositioning:0.0.9"
	implementation "com.vincentbrison.openlibraries.android:dualcache:3.1.1"
	implementation "com.jakewharton.threetenabp:threetenabp:1.0.5"

	// Anko
	implementation "org.jetbrains.anko:anko-commons:$anko_version"
	implementation "org.jetbrains.anko:anko-coroutines:$anko_version"

	// Retrofit
	implementation "com.squareup.retrofit2:retrofit:$retrofit_version"
	implementation "com.squareup.retrofit2:converter-gson:$retrofit_version"

	// Dagger
	implementation "com.google.dagger:dagger:$dagger_version"
	kapt "com.google.dagger:dagger-compiler:$dagger_version"
	compileOnly "javax.annotation:jsr250-api:1.0"

	// Reactive
	implementation "io.reactivex.rxjava2:rxjava:2.1.6"
	implementation "io.reactivex.rxjava2:rxandroid:2.0.1"
	implementation "io.reactivex.rxjava2:rxkotlin:2.1.0"

	// Lifecycle
	implementation "android.arch.lifecycle:extensions:1.0.0-rc1"
	implementation "android.arch.lifecycle:reactivestreams:1.0.0-rc1"


	// Testing
	testImplementation "org.robolectric:robolectric:3.4.2"
	testImplementation "junit:junit:4.12"
	testImplementation "org.mockito:mockito-core:$mockito_version"
	testImplementation "com.nhaarman:mockito-kotlin-kt1.1:$mockito_kotlin_version", { exclude group: "org.jetbrains.kotlin" }
	testImplementation "commons-io:commons-io:$commons_io_version"
	testImplementation "org.assertj:assertj-core:3.8.0"

	// Instrumentation testing
	androidTestImplementation "com.android.support:support-annotations:$android_support_version"
	androidTestImplementation "com.android.support.test:runner:1.0.1"
	androidTestImplementation "com.android.support.test:rules:1.0.1"
	androidTestImplementation "com.android.support.test.espresso:espresso-core:3.0.1"
	androidTestImplementation "org.mockito:mockito-android:$mockito_version"
	androidTestImplementation "com.nhaarman:mockito-kotlin-kt1.1:$mockito_kotlin_version", { exclude group: "org.jetbrains.kotlin" }
	androidTestImplementation "commons-io:commons-io:$commons_io_version"
	kaptAndroidTest "com.google.dagger:dagger-compiler:$dagger_version"
}
