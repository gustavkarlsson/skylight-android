apply plugin: "com.android.application"
apply plugin: "kotlin-android"
apply plugin: "kotlin-kapt"
apply plugin: "kotlin-android-extensions"

android {
	compileSdkVersion 25
	buildToolsVersion "25.0.2"

	sourceSets {
		main.java.srcDirs += "src/main/kotlin"
		debug.java.srcDirs += "src/debug/kotlin"
		release.java.srcDirs += "src/release/kotlin"
		test.java.srcDirs += "src/test/kotlin"
		androidTest.java.srcDirs += "src/androidTest/kotlin"
	}

	packagingOptions {
		// Exclude duplicate files in output
		exclude "META-INF/LICENSE"
		exclude "META-INF/NOTICE"
	}

	lintOptions {
		// Fixes dagger errors
		disable "InvalidPackage"
	}

	testOptions {
		// Solves android Log not mocked
		unitTests.returnDefaultValues = true
	}

	defaultConfig {
		applicationId "se.gustavkarlsson.skylight.android"
		minSdkVersion 19
		targetSdkVersion 25
		versionCode 1
		versionName "1.0"
		testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
	}

	buildTypes {
		release {
			minifyEnabled true
			proguardFiles getDefaultProguardFile("proguard-android.txt"), "proguard-rules.pro"
		}

		debug {
			versionNameSuffix "-debug"
			applicationIdSuffix ".debug"
		}
	}
}

// Stops dagger compiler errors from truncating
gradle.projectsEvaluated {
	tasks.withType(JavaCompile) {
		options.compilerArgs << "-Xmaxerrs" << "500"
	}
}

dependencies {
	ext {
		retrofit_version = "2.2.0"
		dagger_version = "2.10"
		google_play_services_version = "10.2.4"
		android_support_version = "25.3.1"
		anko_version = "0.10.1"
	}
	compile "org.jetbrains.kotlin:kotlin-stdlib-jre7:$kotlin_version"
	compile "com.android.support:appcompat-v7:$android_support_version"
	compile "com.android.support:design:$android_support_version"
	compile "com.android.support.constraint:constraint-layout:1.0.2"
	compile "com.google.android.gms:play-services-location:$google_play_services_version"
	compile "com.google.android.gms:play-services-gcm:$google_play_services_version"

	compile "com.evernote:android-job:1.1.10"
	compile "com.github.gustavkarlsson:aurora-notifier-common:aurora-notifier-common-1.5.0"
	compile "net.e175.klaus:solarpositioning:0.0.9"
	compile "com.vincentbrison.openlibraries.android:dualcache:3.1.1"
	compile "com.jakewharton.threetenabp:threetenabp:1.0.5"
	compile "org.jetbrains.anko:anko-commons:$anko_version"

	// Retrofit
	compile "com.squareup.retrofit2:retrofit:$retrofit_version"
	compile "com.squareup.retrofit2:converter-gson:$retrofit_version"

	// Dagger
	compile "com.google.dagger:dagger:$dagger_version"
	kapt "com.google.dagger:dagger-compiler:$dagger_version"
	provided "javax.annotation:jsr250-api:1.0"

	// Testing
	testCompile "org.robolectric:robolectric:3.3.2"
	testCompile "junit:junit:4.12"
	testCompile "org.mockito:mockito-core:2.8.9"
	testCompile "commons-io:commons-io:2.4"
	testCompile "org.assertj:assertj-core:2.5.0"

	androidTestCompile "com.android.support:support-annotations:$android_support_version"
	androidTestCompile "com.android.support.test:runner:0.5"
	androidTestCompile "com.android.support.test:rules:0.5"
	androidTestCompile "com.android.support.test.espresso:espresso-core:2.2.2"
	kaptAndroidTest "com.google.dagger:dagger-compiler:$dagger_version"
}
