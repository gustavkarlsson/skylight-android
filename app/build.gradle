apply plugin: "com.android.application"
apply plugin: "kotlin-android"
apply plugin: "kotlin-kapt"
apply plugin: "kotlin-android-extensions"

android {
	compileSdkVersion project.compilesdk_version
	buildToolsVersion "27.0.3"

	sourceSets {
		main.java.srcDirs += "src/main/kotlin"
		debug.java.srcDirs += "src/debug/kotlin"
		release.java.srcDirs += "src/release/kotlin"
		test.java.srcDirs += "src/test/kotlin"
		androidTest.java.srcDirs += "src/androidTest/kotlin"
	}

	packagingOptions {
		// Exclude duplicate files in output
		exclude "META-INF/LICENSE"
		exclude "META-INF/NOTICE"
	}

	lintOptions {
		// Fixes dagger errors
		disable "InvalidPackage"
	}

	testOptions {
		// Solves android Log not mocked
		unitTests.returnDefaultValues = true
	}

	defaultConfig {
		applicationId "se.gustavkarlsson.skylight.android"
		minSdkVersion 19
		targetSdkVersion 27
		versionCode 1
		versionName "1.0"
		testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
		multiDexEnabled true
		// TODO https://developer.android.com/studio/build/multidex.html#dev-build
	}

	signingConfigs {
		release {
			storeFile file(project.android_store_file)
			storePassword project.android_store_password
			keyAlias project.android_key_alias
			keyPassword project.android_key_password
		}
	}

	buildTypes {
		release {
			minifyEnabled true
			proguardFiles getDefaultProguardFile("proguard-android.txt"), "proguard-rules.pro"
			signingConfig signingConfigs.release
		}

		debug {
			versionNameSuffix "-debug"
			applicationIdSuffix ".debug"
		}
	}
	buildTypes.each {
		it.buildConfigField 'String', 'OPENWEATHERMAP_API_KEY', "\"$project.openweathermap_api_key\""
	}
}

kotlin {
	experimental {
		coroutines "enable"
	}
}

// Stops dagger compiler errors from truncating
gradle.projectsEvaluated {
	tasks.withType(JavaCompile) {
		options.compilerArgs << "-Xmaxerrs" << "500"
	}
}

dependencies {
	implementation project(":services")
	implementation project(":entities")
	implementation project(":caching")

	implementation "com.android.support:multidex:1.0.1"
	implementation "org.jetbrains.kotlin:kotlin-stdlib-jre7:$kotlin_version"
	implementation "com.android.support:appcompat-v7:$android_support_version"
	implementation "com.android.support:design:$android_support_version"
	implementation "com.android.support:support-annotations:$android_support_version"
	implementation "com.android.support.constraint:constraint-layout:1.0.2"
	implementation "com.google.android.gms:play-services-location:$google_play_services_version"
	implementation "com.google.android.gms:play-services-gcm:$google_play_services_version"

	implementation "com.evernote:android-job:1.2.0"
	implementation "com.github.gustavkarlsson:aurora-notifier-common:aurora-notifier-common-1.5.0"
	implementation "net.e175.klaus:solarpositioning:0.0.9"
	implementation "com.jakewharton.threetenabp:threetenabp:$threetenabp_version"
	implementation "com.hadisatrio:Optional:$optional_version"
	implementation "com.jakewharton.timber:timber:4.7.0"

	// Anko
	implementation "org.jetbrains.anko:anko-commons:$anko_version"
	implementation "org.jetbrains.anko:anko-design:$anko_version"
	implementation "org.jetbrains.anko:anko-coroutines:$anko_version"

	// Retrofit
	implementation "com.squareup.retrofit2:retrofit:$retrofit_version"
	implementation "com.squareup.retrofit2:converter-gson:$retrofit_version"
	implementation "com.squareup.retrofit2:adapter-rxjava2:$retrofit_version"

	// Dagger
	implementation "com.google.dagger:dagger:$dagger_version"
	kapt "com.google.dagger:dagger-compiler:$dagger_version"
	compileOnly "javax.annotation:jsr250-api:1.0"

	// ReactiveX
	implementation "io.reactivex.rxjava2:rxjava:$rxjava_version"
	implementation "io.reactivex.rxjava2:rxandroid:2.0.1"
	implementation "io.reactivex.rxjava2:rxkotlin:2.2.0"

	// Rx misc
	implementation "com.jakewharton.rxrelay2:rxrelay:2.0.0"
	implementation "com.tbruyelle.rxpermissions2:rxpermissions:0.9.5@aar"
	implementation "pl.charmas.android:android-reactive-location2:2.1@aar"
	implementation "com.f2prateek.rx.preferences2:rx-preferences:2.0.0-RC3"
	implementation "com.github.pwittchen:reactivenetwork-rx2:0.12.3"

	// RxBinding
	implementation "com.jakewharton.rxbinding2:rxbinding-kotlin:$rxbinding_version"
	implementation "com.jakewharton.rxbinding2:rxbinding-support-v4-kotlin:$rxbinding_version"
	implementation "com.jakewharton.rxbinding2:rxbinding-appcompat-v7-kotlin:$rxbinding_version"
	implementation "com.jakewharton.rxbinding2:rxbinding-design-kotlin:$rxbinding_version"
	implementation "com.jakewharton.rxbinding2:rxbinding-recyclerview-v7-kotlin:$rxbinding_version"
	implementation "com.jakewharton.rxbinding2:rxbinding-leanback-v17-kotlin:$rxbinding_version"

	// AutoDispose
	implementation "com.uber.autodispose:autodispose-kotlin:$autodispose_version"
	implementation "com.uber.autodispose:autodispose-android-kotlin:$autodispose_version"
	implementation "com.uber.autodispose:autodispose-android-archcomponents-kotlin:$autodispose_version"

	// Lifecycle
	implementation "android.arch.lifecycle:extensions:1.1.0"


	// Testing
	testImplementation "org.robolectric:robolectric:$robolectric_version"
	testImplementation "junit:junit:$junit_version"
	testImplementation "org.mockito:mockito-core:$mockito_version"
	testImplementation "com.nhaarman:mockito-kotlin-kt1.1:$mockito_kotlin_version", { exclude group: "org.jetbrains.kotlin" }
	testImplementation "commons-io:commons-io:$commons_io_version"
	testImplementation "org.assertj:assertj-core:$assertj_version"

	// Instrumentation testing
	androidTestImplementation "com.android.support:support-annotations:$android_support_version"
	androidTestImplementation "com.android.support.test:runner:$android_support_test_version"
	androidTestImplementation "com.android.support.test:rules:$android_support_test_version"
	androidTestImplementation "com.android.support.test.espresso:espresso-core:3.0.1"
	androidTestImplementation "org.mockito:mockito-android:$mockito_version"
	androidTestImplementation "com.nhaarman:mockito-kotlin-kt1.1:$mockito_kotlin_version", { exclude group: "org.jetbrains.kotlin" }
	androidTestImplementation "commons-io:commons-io:$commons_io_version"
	kaptAndroidTest "com.google.dagger:dagger-compiler:$dagger_version"
}
